options
{
    /** 使用jdk1.6 */
    JDK_VERSION = "1.6";

    /** 生成非静态方法 */
    static = false;

    /** 让jjtree给每个非终结符生成一个对象 */
    MULTI = true;

    /** 开启访问者模式 */
    VISITOR = true;
}

PARSER_BEGIN(Parser)
package cc.concurrent.mango.runtime.parser;

import java.io.StringReader;
import java.io.Reader;

public class Parser {
    public Parser(String s) {
        this((Reader)(new StringReader(s)));
    }
}
PARSER_END(Parser)

TOKEN :
{
        <#COLON: ":" >
    |   <#NUMBER: ["1"-"9"] (["0"-"9"])* >
    |   <#DOT: "." >
    |   <#FIELD: ["a"-"z","A"-"Z","_"] (["a"-"z","A"-"Z","_", "0"-"9"])* >
    |   <IN_PARAM: "(" (" ")* <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* (" ")* ")" >
    |   <OUT_PARAM: <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* >
    |   <EXPR_START: "${" > {SwitchTo(G);}
}

TOKEN:
{
        <TEXT: (~[":", "$", "(", "."])+ >
}

<G>
TOKEN:
{
        <PARAM: <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* >
    |   <MINUS: "-">
    |   <PLUS: "+">
    |   <MULTIPLY: "*">
    |   <DIVIDE: "/">
    |   <MODULUS: "%">
    |   <EXPR_END: "}" > {SwitchTo(DEFAULT);}
    |   <INTEGER_LITERAL: ["1"-"9"] (["0"-"9"])* >
    |   <LPAREN: "(" >
    |   <RPAREN: ")" >
}

<G>
SKIP :
{
        " "
}


SimpleNode parse()  #RootNode :
{
}
{
    (Text() | OutParam() | InParam() | Expr())+ <EOF>
    {
        return jjtThis;
    }
}

void OutParam() :
{
    Token t;
}
{
    t=<OUT_PARAM>
    {
        jjtThis.setParam(t.image);
    }
}

void InParam() :
{
    Token t;
}
{
    t=<IN_PARAM>
    {
        jjtThis.setParam(t.image);
    }
}

void Text() :
{
    Token t;
}
{
    t=<TEXT>
    {
        jjtThis.setText(t.image);
    }
}

void Expr() #void : // 标记为void不生成节点
{

}
{
    <EXPR_START> AdditiveExpression() <EXPR_END>
}

void AdditiveExpression() #void : // 标记为void不生成节点
{}
{
    MultiplicativeExpression()
    (
            <PLUS>  MultiplicativeExpression() #AddNode(2)
        |   <MINUS> MultiplicativeExpression() #SubtractNode(2)
    )*
}

void MultiplicativeExpression() #void : // 标记为void不生成节点
{}
{
    PrimaryExpression()
    (
            <MULTIPLY>  PrimaryExpression() #MulNode(2)
        |   <DIVIDE>  PrimaryExpression() #DivNode(2)
        |   <MODULUS>  PrimaryExpression() #ModNode(2)
    )*
}

void PrimaryExpression() #void :
{}
{
        Param()
    |   IntegerLiteral()
    |   <LPAREN> AdditiveExpression() <RPAREN>
}

void Param() :
{
    Token t;
}
{
    t=<PARAM>
    {
        jjtThis.setParam(t.image);
    }
}

void IntegerLiteral() :
{
    Token t;
}
{
    t=<INTEGER_LITERAL>
    {
        jjtThis.setValue(Integer.valueOf(t.image));
    }
}