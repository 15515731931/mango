options
{
    /** 使用jdk1.6 */
    JDK_VERSION = "1.6";

    /** 生成非静态方法 */
    static = false;

    /** 让jjtree给每个非终结符生成一个对象 */
    MULTI = true;

    /** 开启访问者模式 */
    VISITOR = true;

    /** 忽略大小写 */
    IGNORE_CASE = true;
}

PARSER_BEGIN(Parser)
package cc.concurrent.mango.runtime.parser;

import java.io.StringReader;
import java.io.Reader;

public class Parser {
    public Parser(String s) {
        this((Reader)(new StringReader(s)));
    }
}
PARSER_END(Parser)

TOKEN :
{
        <#COLON: ":" >
    |   <#NUMBER: ["1"-"9"] (["0"-"9"])* >
    |   <#DOT: "." >
    |   <#FIELD: ["a"-"z","_"] (["a"-"z","_", "0"-"9"])* >
    |   <#BLANK: (" ")* >
    |   <ITERABLE_PARAMETER: "in" <BLANK> "(" <BLANK> <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* <BLANK> ")" >
    |   <OBJECT_PARAMETER: <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* >
    |   <BLANK_MORE: (" ")+ >
    |   <EXPR_START: "${" > {SwitchTo(G);}
}

TOKEN:
{
        <TEXT: (~[":", "$", " "])+ >
}

<G>
TOKEN:
{
        <MATH_PARAMETER: <COLON> (<NUMBER>|<FIELD>) (<DOT> <FIELD>)* >
    |   <MINUS: "-">
    |   <PLUS: "+">
    |   <MULTIPLY: "*">
    |   <DIVIDE: "/">
    |   <MODULUS: "%">
    |   <EXPR_END: "}" > {SwitchTo(DEFAULT);}
    |   <INTEGER_LITERAL: ["1"-"9"] (["0"-"9"])* >
    |   <LPAREN: "(" >
    |   <RPAREN: ")" >
}

<G>
SKIP :
{
        " "
}


ASTRootNode parse()  #RootNode :
{
}
{
    (Text() | Blank() | NonIterableParameter() | IterableParameter() | ValuableExpression())+ <EOF>
    {
        return jjtThis;
    }
}

void NonIterableParameter() :
{
    Token t;
}
{
    t=<OBJECT_PARAMETER>
    {
        jjtThis.setParam(t.image);
    }
}

void IterableParameter() :
{
    Token t;
}
{
    t=<ITERABLE_PARAMETER>
    {
        jjtThis.setParam(t.image);
    }
}

void Text() :
{
    Token t;
}
{
    t=<TEXT>
    {
        jjtThis.setText(t.image);
    }
}

void Blank() :
{
    Token t;
}
{
    t=<BLANK_MORE>
    {
        jjtThis.setBlank(t.image);
    }
}

void ValuableExpression() #void : // 标记为void不生成节点
{

}
{
    <EXPR_START> AdditiveExpression() <EXPR_END>
}

void AdditiveExpression() #void : // 标记为void不生成节点
{}
{
    MultiplicativeExpression()
    (
            <PLUS>  MultiplicativeExpression() #AddNode(2)
        |   <MINUS> MultiplicativeExpression() #SubtractNode(2)
    )*
}

void MultiplicativeExpression() #void : // 标记为void不生成节点
{}
{
    PrimaryExpression()
    (
            <MULTIPLY>  PrimaryExpression() #MulNode(2)
        |   <DIVIDE>  PrimaryExpression() #DivNode(2)
        |   <MODULUS>  PrimaryExpression() #ModNode(2)
    )*
}

void PrimaryExpression() #void :
{}
{
        Variable()
    |   IntegerLiteral()
    |   <LPAREN> AdditiveExpression() <RPAREN>
}

void Variable() :
{
    Token t;
}
{
    t=<MATH_PARAMETER>
    {
        jjtThis.setParam(t.image);
    }
}

void IntegerLiteral() :
{
    Token t;
}
{
    t=<INTEGER_LITERAL>
    {
        jjtThis.setValue(Integer.valueOf(t.image));
    }
}